package smarthomeMirage
public
	
	-- imports --
	with SEI;
	with Data_Model;
	with Base_Types;
	-- end imports --
	
	-- data types
	data IsMovement
		properties
			Data_Model::Data_Representation => Boolean;
			Data_Size => 1 Bits;
    end IsMovement;
    
    data LightIntensity
    	properties
    		Data_Model::Data_Representation => Fixed;
    		Data_Model::Measurement_Unit => "Lux";
    		Data_Size => 12 Bits;
    end LightIntensity;
	
	data SwitchLight
		properties
			Data_Model::Enumerators => ("On", "Off", "Auto");
			Data_Size => 2 Bits;
    end SwitchLight;
    
    data LightPercentage
    	properties
    		Data_Model::Data_Representation => Integer;
    		Data_Model::Measurement_Unit => "Percent";
    		Data_Size => 10 Bits;
    end LightPercentage;
    
    
    data Temperature
    	properties
    		Data_Model::Data_Representation => Fixed;
    		Data_Model::Measurement_Unit => "degree Celsius";
    		Data_Size => 8 Bits;
    end Temperature;

    data Humidity
        properties
            Data_Model::Data_Representation => Integer;
            Data_Model::Measurement_Unit => "Percent";
    		Data_Size => 7 Bits;
    end Humidity;
    
    data SwitchTemperature
   		properties
    		Data_Model::Enumerators => ("Sleep", "Fan", "Eco", "Normal", "Turbo");
    		Data_Model::Data_Representation => Fixed;
            Data_Model::Measurement_Unit => "degree Celsius";
    		Data_Size => 11 Bits;	
    end SwitchTemperature;
    
    data TemperatureAdjustment
    	properties
        	Data_Model::Data_Representation => Integer;
        	Data_Model::Integer_Range => -100 .. 100;
        	Data_Size => 8 Bytes;
    end TemperatureAdjustment;
    

    data SwitchAlarm
        properties
            Data_Model::Data_Representation => Boolean;
            Data_Size => 1 Bits;
    end SwitchAlarm;

    data TurnOn
        properties
            Data_Model::Data_Representation => Boolean;
            Data_Size => 1 Bits;
    end TurnOn;

	-- end data types --
	
	-- processes --
	
    process LightController
    	features
            receive_movement_in: in data port IsMovement;
            receive_luminance_in: in data port LightIntensity;
            receive_switch_in: in data port SwitchLight;
            send_light_percent_out: out data port LightPercentage;
            
    end LightController;

    process implementation LightController.impl
        subcomponents
            sensorsThread: thread LightSensorsThread.impl;
            switchThread: thread LightSwitchThread.impl;
        connections
            c1: port receive_luminance_in -> sensorsThread.luminance_in;
            c2: port receive_movement_in -> sensorsThread.movement_in;
            c3: port receive_switch_in -> switchThread.light_switch_in;
            c4: port sensorsThread.light_percent_out -> switchThread.light_percent_in;
            c5: port switchThread.light_percent_out -> send_light_percent_out;
            
    end LightController.impl;


    process AirConditionerController
		features
            receive_humidity_in: in data port Humidity;
            receive_temperature_in: in data port Temperature;
            receive_switch_temperature_in: in data port SwitchTemperature;
            send_temperature_adjustment_out: out data port TemperatureAdjustment;
    end AirConditionerController;

    process implementation AirConditionerController.impl
		subcomponents
            airconditionerThread: thread AirConditionerThread.impl;
        connections
            c1: port receive_humidity_in -> airconditionerThread.humidity_in;
            c2: port receive_temperature_in -> airconditionerThread.temperature_in;
            c3: port receive_switch_temperature_in -> airconditionerThread.switch_temperature_in;
            c4: port airconditionerThread.temperature_adjustment_out -> send_temperature_adjustment_out;
    end AirConditionerController.impl;
    
    
    process AlarmController
        features
            receive_movement_in: in data port IsMovement;
            receive_switch_in : in data port SwitchAlarm;
            send_turn_on_out: out data port TurnOn;
    end AlarmController;

    process implementation AlarmController.impl
		subcomponents
            alarmThread: thread AlarmThread.impl;
        connections
            c1: port receive_movement_in -> alarmThread.movement_in;
            c2: port receive_switch_in -> alarmThread.alarm_switch_in;
            c3: port alarmThread.turn_on_out -> send_turn_on_out;
    end AlarmController.impl;
	
	-- end processes --
	
	-- threads --
	
    thread LightSensorsThread
        features
            movement_in: in data port IsMovement;
            luminance_in: in data port LightIntensity;
            light_percent_out: out data port LightPercentage;
        properties
            SEI::MIPSBudget => 10.0 mips;
            Thread_Properties::Dispatch_Protocol => Periodic;
			Timing_Properties::Period => 1000ms;
    end LightSensorsThread;

    thread implementation LightSensorsThread.impl
    end LightSensorsThread.impl;

    thread LightSwitchThread
        features
            light_switch_in: in data port SwitchLight;
            light_percent_in: in data port LightPercentage;
            light_percent_out: out data port LightPercentage;
        properties
            SEI::MIPSBudget => 5.0 mips;
            Thread_Properties::Dispatch_Protocol => Aperiodic;
            Timing_Properties::Period => 50ms;
    end LightSwitchThread;

    thread implementation LightSwitchThread.impl
    end LightSwitchThread.impl;
    

    thread AirConditionerThread
        features
            humidity_in: in data port Humidity;
            temperature_in: in data port Temperature;
            switch_temperature_in: in data port SwitchTemperature;
            temperature_adjustment_out: out data port TemperatureAdjustment;
        properties
            SEI::MIPSBudget => 15.0 mips;
            Thread_Properties::Dispatch_Protocol => Aperiodic;
			Timing_Properties::Period => 50ms;
    end AirConditionerThread;

    thread implementation AirConditionerThread.impl
    end AirConditionerThread.impl;
    
    
    thread AlarmThread
        features
            movement_in: in data port IsMovement;
            alarm_switch_in: in data port SwitchAlarm;
            turn_on_out: out data port TurnOn;
        properties
            SEI::MIPSBudget => 5.0 mips;
            Thread_Properties::Dispatch_Protocol => Aperiodic;
            Timing_Properties::Period => 100ms; 
    end AlarmThread;

    thread implementation AlarmThread.impl
    end AlarmThread.impl;
	
	-- end threads --
	
	-- devices --
	
    device MovementSensor
    	features
    		movement_out: out data port IsMovement;
			hc_access: requires bus access HomeConnection;
    	properties
    		SEI::GrossWeight => 0.3 kg;
			Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Periodic;
    end MovementSensor;

	device implementation MovementSensor.impl
	end MovementSensor.impl;

    device LuminanceSensor
		features 
			luminance_out: out data port LightIntensity;
			hc_access: requires bus access HomeConnection;
    	properties
    		SEI::GrossWeight => 0.2 kg;
			Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Periodic;
    end LuminanceSensor;
	
	device implementation LuminanceSensor.impl
	end LuminanceSensor.impl;

    device LightSwitch
    	features
    		switch_out: out data port SwitchLight;
			hc_access: requires bus access HomeConnection;
			ethernet_access: requires bus access Ethernet;
    	properties
    		SEI::GrossWeight => 0.1 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end LightSwitch;

	device implementation LightSwitch.impl
	end LightSwitch.impl;

    device LightDevice
    	features 
    		light_in: in data port LightPercentage;
			hc_access: requires bus access HomeConnection;
    	properties
			SEI::GrossWeight => 0.2 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end LightDevice;

	device implementation LightDevice.impl
	end LightDevice.impl;
    

    device HumiditySensor
    	features
    		humidity_out: out data port Humidity;
			hc_access: requires bus access HomeConnection;
    	properties
    		SEI::GrossWeight => 0.1 kg;
    		Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Periodic;
    end HumiditySensor;

	device implementation HumiditySensor.impl
	end HumiditySensor.impl;

    device TemperatureSensor
		features
    		temperature_out: out data port Temperature;
			hc_access: requires bus access HomeConnection;
    	properties
    		SEI::GrossWeight => 0.2 kg;
    		Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Periodic;
    end TemperatureSensor;

	device implementation TemperatureSensor.impl
	end TemperatureSensor.impl;

    device TemperatureSwitch
		features
    		switch_out: out data port SwitchTemperature;
			hc_access: requires bus access HomeConnection;
			ethernet_access: requires bus access Ethernet;
    	properties
    		SEI::GrossWeight => 0.1 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end TemperatureSwitch;

    device implementation TemperatureSwitch.impl
    end TemperatureSwitch.impl;

    device AirConditioner
		features 
    		temperature_in: in data port TemperatureAdjustment;
			hc_access: requires bus access HomeConnection;
    	properties
			SEI::GrossWeight => 8.0 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end AirConditioner;

    device implementation AirConditioner.impl
    end AirConditioner.impl;


    device AlarmSwitch
		features
    		switch_out: out data port SwitchAlarm;
			hc_access: requires bus access HomeConnection;
			ethernet_access: requires bus access Ethernet;
    	properties
    		SEI::GrossWeight => 0.1 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end AlarmSwitch;

    device implementation AlarmSwitch.impl
    end AlarmSwitch.impl;

    device Speaker
		features 
    		turn_on_in: in data port TurnOn;
			hc_access: requires bus access HomeConnection;
    	properties
			SEI::GrossWeight => 1.0 kg;
			-- Timing_Properties::Period => 50ms;
			Thread_Properties::Dispatch_Protocol => Aperiodic;
    end Speaker;

    device implementation Speaker.impl
    end Speaker.impl;
	
	-- end devices --
	
	-- bus, memory, processor --
	
	bus Ethernet
		properties
			SEI::GrossWeight => 0.15kg;
			SEI::BandWidthCapacity => 100.0 Mbps;
	end Ethernet;

	bus implementation Ethernet.impl
	end Ethernet.impl;
	
	bus HomeConnection
	properties
			SEI::GrossWeight => 0.15kg;
			SEI::BandWidthCapacity => 1000.0 Mbps;
	end HomeConnection;

	bus implementation HomeConnection.impl
	end HomeConnection.impl;

	memory RAM
		features
			hc_access: requires bus access HomeConnection;
		properties
			SEI::GrossWeight => 0.03kg;
	end RAM;

	memory implementation RAM.impl
	end RAM.impl;
	
	processor CPU
	  features
	    hc_access: requires bus access HomeConnection;
		properties
			Clock_Period => 1 ms;
			Timing_Properties::Processor_Capacity => 5.0 mips;
			SEI::MIPSCapacity => 100.0 mips;
			SEI::NetWeight => 0.1 kg;
	end CPU;

	processor implementation CPU.impl	end CPU.impl;
	
	-- end bus, memory, processor --
	
	-- system --
	
	system SmartHomeMirage
	end SmartHomeMirage;
	
	system implementation SmartHomeMirage.impl
		
		subcomponents
            lightController: process LightController.impl;
            airConditionerController: process AirConditionerController.impl;
            alarmController: process AlarmController.impl;

            ethernet: bus Ethernet.impl;
            homeConnection: bus HomeConnection.impl;
            ram: memory RAM.impl;
            cpu: processor CPU.impl;

            movementSensor: device MovementSensor.impl;
            luminanceSensor: device LuminanceSensor.impl;
            lightSwitch: device LightSwitch.impl;
            lightDevice: device LightDevice.impl;

            humiditySensor: device HumiditySensor.impl;
            temperatureSensor: device TemperatureSensor.impl;
            temperatureSwitch: device TemperatureSwitch.impl;
            airConditioner: device AirConditioner.impl;

            alarmSwitch: device AlarmSwitch.impl;
            speaker: device Speaker.impl;
        connections
            c1: port movementSensor.movement_out -> lightController.receive_movement_in;
            c2: port luminanceSensor.luminance_out -> lightController.receive_luminance_in;
            c3: port lightSwitch.switch_out -> lightController.receive_switch_in;
            c4: port lightController.send_light_percent_out -> lightDevice.light_in;

            c5: port humiditySensor.humidity_out -> airConditionerController.receive_humidity_in;
            c6: port temperatureSensor.temperature_out -> airConditionerController.receive_temperature_in;
            c7: port temperatureSwitch.switch_out -> airConditionerController.receive_switch_temperature_in;
            c8: port airConditionerController.send_temperature_adjustment_out -> airConditioner.temperature_in;

            c9: port movementSensor.movement_out -> alarmController.receive_movement_in;
            c10: port alarmSwitch.switch_out -> alarmController.receive_switch_in;
            c11: port alarmController.send_turn_on_out -> speaker.turn_on_in;

            -- bus connections --
            processor_bus: bus access homeConnection <-> cpu.hc_access;
            ram_bus: bus access homeConnection <-> ram.hc_access;
            
            movementsensor_bus: bus access homeConnection <-> movementSensor.hc_access;
            luminancesensor_bus: bus access homeConnection <-> luminanceSensor.hc_access;
            lightswitch_bus: bus access homeConnection <-> lightSwitch.hc_access;
            lightswitch_ethernet: bus access ethernet <-> lightSwitch.ethernet_access;
            lightdevice_bus: bus access homeConnection <-> lightDevice.hc_access;

            humiditysensor_bus: bus access homeConnection <-> humiditySensor.hc_access;
            temperaturesensor_bus: bus access homeConnection <-> temperatureSensor.hc_access;
            temperatureswitch_bus: bus access homeConnection <-> temperatureSwitch.hc_access;
            temperatureswitch_ethernet: bus access ethernet <-> temperatureSwitch.ethernet_access;
            airconditioner_bus: bus access homeConnection <-> airConditioner.hc_access;
            
            alarmswitch_bus: bus access homeConnection <-> alarmSwitch.hc_access;
            alarmswitch_ethernet: bus access ethernet <-> alarmSwitch.ethernet_access;
            speaker_bus: bus access homeConnection <-> speaker.hc_access;
        
        properties
	    	Actual_Connection_Binding => (reference(HomeConnection)) applies to processor_bus;
	    	Actual_Connection_Binding => (reference(HomeConnection)) applies to ram_bus;
            
			Actual_Connection_Binding => (reference(HomeConnection)) applies to movementsensor_bus;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to luminancesensor_bus;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to lightswitch_bus;
            Actual_Connection_Binding => (reference(Ethernet)) applies to lightswitch_ethernet;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to lightdevice_bus;
            
			Actual_Connection_Binding => (reference(HomeConnection)) applies to humiditysensor_bus;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to temperaturesensor_bus;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to temperatureswitch_bus;
            Actual_Connection_Binding => (reference(Ethernet)) applies to temperatureswitch_ethernet;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to airconditioner_bus;
            
			Actual_Connection_Binding => (reference(HomeConnection)) applies to alarmswitch_bus;
			Actual_Connection_Binding => (reference(Ethernet)) applies to alarmswitch_ethernet;
			Actual_Connection_Binding => (reference(HomeConnection)) applies to speaker_bus;
		
			Actual_Processor_Binding => (reference(cpu)) applies to lightController;
			Actual_Processor_Binding => (reference(cpu)) applies to airConditionerController;
			Actual_Processor_Binding => (reference(cpu)) applies to alarmController;
			
			SEI::WeightLimit => 20.0kg;
			SEI::MIPSCapacity => 120.0 mips;
			
	end SmartHomeMirage.impl;
		
	-- end system -- 

end smarthomeMirage;
